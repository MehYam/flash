<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" 
		 xmlns:mx="library://ns.adobe.com/flex/mx" 
		 xmlns:local="*"
>
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	<s:Rect radiusX="5" radiusY="5" left="0" right="0" top="0" bottom="0">
		<s:stroke><s:SolidColorStroke color="0xffffff" alpha="0.4" weight="3"/></s:stroke>
		<s:fill><s:SolidColor color="0xffffff" alpha="0.2"/></s:fill>
	</s:Rect>
	<s:Group id="letterParent">
		<s:layout>
			<s:BasicLayout/>
		</s:layout>
	</s:Group>
	<fx:Script>
		<![CDATA[
			import mx.core.UIComponent;
			import mx.states.OverrideBase;
			
			import spark.components.Label;
			import spark.filters.DropShadowFilter;
			
			private static const tileStyle:TextFormat = new TextFormat;
			private static const ROWS:int = 5;
			private static const COLS:int = 5;

			private static const DROP:Array = [ new DropShadowFilter(2, 45, 0xffff00, 0.5, 1, 1) ];
			private var _letters:LetterTable = new LetterTable; //KAI: should be passed in as a data provider
			static private const LETTER_MARGIN:Number = 25;
			static private const TILE_SIZE:Number = 65;
			protected override function createChildren():void
			{
				super.createChildren();
				
				_letters.addEventListener(LetterTableEvent.PATH_CHANGED, onPathChanged, false, 0, true);
				_letters.init(ROWS, COLS);

				for (var i:int = 0; i < (ROWS*COLS); ++i)
				{
					var label:Label = new Label;
					label.text = _letters.getLetterLinear(i);
					label.styleName = "letterTileStyle";
					
					label.x = LETTER_MARGIN + TILE_SIZE * (i % COLS);
					label.y = LETTER_MARGIN + TILE_SIZE * int(i / COLS);
					
					label.filters = DROP;

					letterParent.addElement(label);
				}
			}
			protected override function measure():void
			{
				super.measure();
				
				measuredWidth += LETTER_MARGIN;
				measuredHeight += LETTER_MARGIN;
			}
			
			public function get letters():LetterTable
			{
				return _letters;
			}
			
			private function onPathChanged(e:Event):void
			{
				if (ROWS*COLS <= letterParent.numElements)
				{
					for (var i:int = 0; i < (ROWS*COLS); ++i)
					{
						var label:UIComponent = UIComponent(letterParent.getElementAt(i));
						label.styleName = _letters.isMarked(i) ? "letterTileStyleMarked" : "letterTileStyle";
					}
				}
			}
		]]>
	</fx:Script>
</s:Group>
