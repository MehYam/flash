<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" 
		 xmlns:mx="library://ns.adobe.com/flex/mx"
		 creationComplete="onInit()">
	<fx:Declarations>
		<s:CurrencyFormatter id="formatter" useCurrencySymbol="true"/>
	</fx:Declarations>
	<s:DataGrid
		id="grid"
		width="100%" height="100%" 
		sortableColumns="false"
		editable="true"
	>
		<s:columns>
			<s:ArrayList>
				<s:GridColumn dataField="name" headerText="Item" editable="false"/>
				<s:GridColumn dataField="id" headerText="ID" editable="false"/>
				<s:GridColumn dataField="colors" headerText="Style"/>
				<s:GridColumn dataField="price" headerText="Price" formatter="{formatter}"/>
				<s:GridColumn dataField="count" labelFunction="countLabelFunction" headerText="No."/>
				<s:GridColumn labelFunction="totalLabelFunction" headerText="Total" formatter="{formatter}" editable="false"/>
			</s:ArrayList>
		</s:columns>
	</s:DataGrid>
	<fx:Script>
		<![CDATA[
			import mx.collections.ArrayCollection;
			private var _items:ArrayCollection = new ArrayCollection([]);
			private function onInit():void
			{
				grid.dataProvider = _items;
			}
			static private function totalLabelFunction(itemObj:Object, column:GridColumn):String
			{
				return column.formatter.format(itemObj.price * itemObj.count);
			}
			static private function countLabelFunction(itemObj:Object, column:GridColumn):String
			{
				// a number formatter would be a cooler way of doing this, and should be more immune to things like sorting, etc
				return "x" + itemObj.count;
			}
			public function clear():void
			{
				_items.removeAll();
			}
			public function addItem(item:Object):void
			{
				// increment the number on the last item if it's the same
				var copy:Object =  // KAI: better way to do this, in my library
					{
						name: item.name,
						id: item.id,
						count: 1,
						price: item.price
					};
				_items.addItem(copy);
				_items.refresh();
				
				grid.selectedIndex = _items.length - 1;
				grid.ensureCellIsVisible(_items.length - 1);
			}
			public function incItem(index:int):void
			{
				var item:Object = _items.getItemAt(index);
				++item.count;
				
				_items.refresh();
			}
			public function decItem(index:int):void
			{
				var item:Object = _items.getItemAt(index);
				--item.count;

				if (item.count <= 0)
				{
					_items.removeItemAt(index);
				}
				_items.refresh();
			}
			public function addProperty(prop:String):void
			{
				var item:Object = grid.selectedItem;
				if (item.colors)
				{
					if (item.colors.indexOf(prop) == -1)
					{
						item.colors += ", " + prop;
					}
				}
				else
				{
					item.colors = prop;
				}
				_items.refresh();
			}
			public function get total():Number
			{
				var retval:Number = 0;
				for each (var item:Object in _items.source)
				{
					retval += (item.price * item.count);
				}
				return retval;
			}
			public function get numItems():int
			{
				var retval:Number = 0;
				for each (var item:Object in _items.source)
				{
					retval += item.count;
				}
				return retval;
			}
			public function get selectedIndex():int
			{
				return grid.selectedIndex;
			}
			public function get items():Array
			{
				return _items.toArray();
			}
			public function set items(i:Array):void
			{
				_items.source = i;
				_items.refresh();
			}
		]]>
	</fx:Script>
</s:Group>
