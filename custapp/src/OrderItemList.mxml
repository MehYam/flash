<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" 
		 xmlns:mx="library://ns.adobe.com/flex/mx"
		 creationComplete="onInit()">
	<fx:Declarations>
		<s:CurrencyFormatter id="formatter" useCurrencySymbol="true"/>
	</fx:Declarations>
	<s:DataGrid
		id="grid"
		width="100%" height="100%" 
		sortableColumns="true"
	>
		<s:columns>
			<s:ArrayList>
				<s:GridColumn dataField="name" headerText="Item"/>
				<s:GridColumn dataField="id" headerText="ID"/>
				<s:GridColumn dataField="colors" headerText="Style"/>
				<s:GridColumn dataField="price" headerText="Price" formatter="{formatter}"/>
			</s:ArrayList>
		</s:columns>
	</s:DataGrid>
	<fx:Script>
		<![CDATA[
			import mx.collections.ArrayCollection;
			private var _items:ArrayCollection = new ArrayCollection([]);
			private function onInit():void
			{
				grid.dataProvider = _items;
			}
			public function addItem(item:Object):void
			{
				var copy:Object =  // KAI: better way to do this, in my library
					{
						name: item.name,
						id: item.id,
						price: item.price
					};
				_items.addItem(copy);
				_items.refresh();
				
				grid.selectedIndex = _items.length - 1;
				grid.ensureCellIsVisible(_items.length - 1);
			}
			public function removeItem(index:int):void
			{
				if (index >= 0)
				{
					_items.removeItemAt(index);
					_items.refresh();
				}
			}
			public function addProperty(prop:String):void
			{
				var item:Object = grid.selectedItem;
				if (item.colors)
				{
					if (item.colors.indexOf(prop) == -1)
					{
						item.colors += ", " + prop;
					}
				}
				else
				{
					item.colors = prop;
				}
				_items.refresh();
			}
			public function get total():Number
			{
				var retval:Number = 0;
				for each (var item:Object in _items.source)
				{
					retval += item.price;
				}
				return retval;
			}
			public function get numItems():int
			{
				return _items.length;
			}
			public function get selectedIndex():int
			{
				return grid.selectedIndex;
			}
			public function get items():Array
			{
				return _items.toArray();
			}
		]]>
	</fx:Script>
</s:Group>
