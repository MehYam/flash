<?xml version="1.0" encoding="utf-8"?>
<s:VGroup xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" 
		 xmlns:mx="library://ns.adobe.com/flex/mx"
		 paddingBottom="10" paddingTop="10" paddingLeft="30" paddingRight="30"
		 >
	<fx:Declarations>
		<fx:Number id="total">0</fx:Number>
		<fx:Number id="owing">0</fx:Number>
		<fx:Number id="paying">0</fx:Number>
	</fx:Declarations>
	<s:VGroup width="100%">
		<s:TileGroup requestedColumnCount="5" id="buttonsCents" width="100%" rowHeight="{Data.BAR_HEIGHT + 20}" columnAlign="justifyUsingWidth"/>
		<s:TileGroup id="buttonsDollars" width="100%" rowHeight="{Data.BAR_HEIGHT + 20}" columnAlign="justifyUsingWidth"/>		
	</s:VGroup>
	<s:HGroup width="100%" horizontalAlign="right">
		<s:Label text="Order Total:"/>
		<s:Label id="totalLabel" text="{Utils.currencyFormatter.format(total)}" styleName="numberLabel"/>
	</s:HGroup>
	<s:HGroup width="100%" horizontalAlign="right">
		<s:Label text="Unpaid:"/>
		<s:Label id="owingLabel"  text="{Utils.currencyFormatter.format(owing)}" styleName="numberLabel"/>
	</s:HGroup>
	<s:HGroup width="100%" horizontalAlign="right">
		<s:Label text="Change:"/>
		<s:Label id="changeLabel" color="0xdd0000" text="{Utils.currencyFormatter.format(paying - owing)}" styleName="numberLabel"/>
	</s:HGroup>
	<s:HGroup width="100%">
		<s:Label text="Paying:" height="100%" verticalAlign="middle"/>
		<s:TextInput id="payingInput" width="100%" color="0x0000ff" fontSize="22" fontWeight="bold" textAlign="right" text="{Utils.currencyFormatter.format(paying)}" keyDown="onKeyDown(event)" focusIn="onFocusIn()" focusOut="updateTotal()"/>
	</s:HGroup>
	<fx:Script>
		<![CDATA[
			import data.Data;
			import data.Order;
			
			import flash.globalization.CurrencyParseResult;
			
			import spark.components.Button;
			import spark.primitives.Rect;
			
			public function set order(o:Order):void
			{
				totalLabel.text = Utils.currencyFormatter.format(o.total);
				owingLabel.text = Utils.currencyFormatter.format(o.total - o.paid);
			}
			static private const BUTTONS_CENTS:Array = 
			[
				{ amount: 0.01 },
				{ amount: 0.05 },
				{ amount: 0.1 },
				{ amount: 0.25 },
			];
			static private const BUTTONS_DOLLARS:Array = 
				[
					{ amount: 1 },
					{ amount: 5 },
					{ amount: 10 },
					{ amount: 20 },
					{ amount: 50 }
				];
			protected override function createChildren():void
			{
				super.createChildren();

				for each (var b:Object in BUTTONS_CENTS)
				{
					button = new Button;
					button.label = "+" + b.amount*100 + "c";
					button.name = String(b.amount);
					button.addEventListener(MouseEvent.CLICK, onAddButton, false, 0, true);
					
					buttonsCents.addElement(button);
				}
				for each (var b2:Object in BUTTONS_DOLLARS)
				{
					button = new Button;
					button.label = "+$" + b2.amount;
					button.name = String(b2.amount);
					button.addEventListener(MouseEvent.CLICK, onAddButton, false, 0, true);
					
					buttonsDollars.addElement(button);
				}
				buttonsDollars.addElement(new Rect);
				buttonsDollars.addElement(new Rect);
				buttonsDollars.addElement(new Rect);
				
				var button:Button;
				button = new Button;
				button.label = "Exact";
				buttonsDollars.addElement(button);
				button.addEventListener(MouseEvent.CLICK, onExactAmount, false, 0, true);
				
				button = new Button;
				button.label = "Clear";
				button.setStyle("color", 0xdd0000);
				buttonsDollars.addElement(button);
				button.addEventListener(MouseEvent.CLICK, onClearAmount, false, 0, true);
				
			}
			private function onAddButton(e:Event):void
			{
				paying += parseFloat(DisplayObject(e.currentTarget).name);
			}
			private function onExactAmount(e:Event):void
			{
				paying = owing;
			}
			private function onClearAmount(e:Event):void
			{
				paying = 0;
			}
			private function onKeyDown(e:KeyboardEvent):void
			{
				if (e.keyCode == Keyboard.ENTER)
				{
					setFocus();
				}
			}
			private var _prevValue:Number = 0;
			private function onFocusIn():void
			{
				_prevValue = paying;
			}
			private function updateTotal():void
			{
				var result:CurrencyParseResult = Utils.currencyFormatter.parse(payingInput.text);
				if (!isNaN(result.value))
				{
					if (paying == result.value)
					{
						// need to kick the binding to make it work else it leaves the old value
						paying = result.value + 1;
						paying = result.value;
					}
					else
					{
						paying = result.value;
					}
				}
				else
				{
					paying = _prevValue + 1;
					paying = _prevValue;
				}
			}
		]]>
	</fx:Script>
</s:VGroup>
