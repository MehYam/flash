<?xml version="1.0" encoding="utf-8"?>
<s:VGroup xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" 
		 xmlns:mx="library://ns.adobe.com/flex/mx"
		 creationComplete="onInit()" xmlns:local="*">
	<fx:Declarations>
		<s:CurrencyFormatter id="formatter" useCurrencySymbol="true"/>
	</fx:Declarations>
	<s:HGroup>
		
	</s:HGroup>
	<s:HGroup>
		<s:TileGroup id="itemButtons" horizontalGap="1" verticalGap="1"/>
		<s:TileGroup id="colorButtons" horizontalGap="1" verticalGap="1"/>
	</s:HGroup>
	<s:HGroup>
		<s:Label text="Items:"/>
		<s:Label id="items" text="0" fontWeight="bold"/>
		<s:Label text="Total:"/>
		<s:Label id="total" text="0" fontWeight="bold"/>
	</s:HGroup>
	<local:ItemList id="order" width="100%" height="100%"/>
	<fx:Script>
		<![CDATA[
			import flash.text.engine.FontWeight;
			
			import mx.binding.utils.BindingUtils;
			import mx.core.IFlexDisplayObject;
			import mx.core.IUIComponent;
			
			import spark.components.Button;
			
			import ui.MultilineButtonSkin;
			
			private var _clearButton:IUIComponent;
			private function onInit():void
			{
				for each (var item:Object in Data.instance.items)
				{
					var button:Button = new Button;
					button.setStyle("skinClass", MultilineButtonSkin);
					button.label = item.name + "\n" + formatter.format(item.price);
					button.height = 100;
					button.width = 100;
					button.name = item.id;
					button.addEventListener(MouseEvent.CLICK, onItem, false, 0, true);

					itemButtons.addElement(button);
				}
				var clearButton:Button = new Button;
				clearButton.height = 100;
				clearButton.width = 100;
				clearButton.setStyle("color", 0xff0000);
				clearButton.setStyle("fontWeight", FontWeight.BOLD);
				clearButton.label = "REMOVE";
				
				clearButton.addEventListener(MouseEvent.CLICK, onRemoveItem, false, 0, true);
				itemButtons.addElement(clearButton);
				_clearButton = clearButton;
				
				BindingUtils.bindSetter(onSelectionUpdate, order.grid, "selectedIndex");
				
				for each (var color:Object in Data.instance.colors.source)
				{
					var colorButton:Button = new Button;
					colorButton.setStyle("color", color.color);
					colorButton.setStyle("fontWeight", FontWeight.BOLD);
					colorButton.label = color.name;
					colorButton.height = 50;
					colorButton.width = 100;
					colorButton.name = color.name;
					colorButton.addEventListener(MouseEvent.CLICK, onColorProperty, false, 0, true);
					
					colorButtons.addElement(colorButton);
				}
				for each (var pattern:String in Data.instance.patterns.source)
				{
					var patternButton:Button = new Button;
					patternButton.setStyle("fontSize", 12);
					patternButton.label = pattern;
					patternButton.height = 50;
					patternButton.width = 100;
					patternButton.name = pattern;
					patternButton.addEventListener(MouseEvent.CLICK, onColorProperty, false, 0, true);
					
					colorButtons.addElement(patternButton);
				}
			}
			private function onSelectionUpdate(foo:int):void
			{
				_clearButton.enabled = colorButtons.enabled = foo >= 0;
			}
			private function onItem(e:Event):void
			{
				for each (var item:Object in Data.instance.items.source)
				{
					if (item.id == parseInt(e.target.name))
					{
						order.addItem(item);
						updateTotal();
						break;
					}
				}
			}
			private function onRemoveItem(e:Event):void
			{
				order.removeItem(order.selectedIndex);
				order.grid.selectedIndex = order.grid.dataProvider.length - 1;
				
				updateTotal();
				
				// shouldn't have to do this because we're binding to the selectedIndex property,
				// but for some reason that property doesn't fire when it gets set to -1
				if (!order.numItems)
				{
					onSelectionUpdate(-1);
				}
			}
			private function onColorProperty(e:Event):void
			{
				const color:String = IFlexDisplayObject(e.target).name;
				order.addProperty(color);
			}
			private function updateTotal():void
			{
				items.text = String(order.numItems);
				total.text = formatter.format(order.total);
			}
		]]>
	</fx:Script>
</s:VGroup>
