<?xml version="1.0" encoding="utf-8"?>
<s:VGroup xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" 
		 xmlns:mx="library://ns.adobe.com/flex/mx"
		 xmlns:local="*"
		 paddingBottom="5"
		 paddingTop="5"
		 paddingLeft="5"
		 paddingRight="5"
		 creationComplete="onInit()" 
		 >
	<fx:Declarations>
	</fx:Declarations>
	<s:HGroup>
		<s:TextInput id="customer" width="300" fontWeight="bold" editable="false" selectable="false" prompt="Select a customer" click="onSelectCustomer()" height="100%"/>
		<s:Line height="100%">
			<s:stroke><s:SolidColorStroke color="#aaaaaa" weight="1"/></s:stroke>
		</s:Line>
		<s:Label text="Pickup Date:" height="100%" verticalAlign="middle"/>
		<mx:DateField id="date" width="160" fontFamily="Arial" fontSize="10" scaleX="2" scaleY="2"/>
		<s:Line height="100%">
			<s:stroke><s:SolidColorStroke color="#aaaaaa" weight="1"/></s:stroke>
		</s:Line>
		<s:Label text="Pickup Time:" height="100%" verticalAlign="middle"/>
		<s:DropDownList id="time" selectedIndex="4" height="100%">
			<s:dataProvider>
				<mx:ArrayList>
					<fx:String>9am</fx:String>
					<fx:String>10am</fx:String>
					<fx:String>11am</fx:String>
					<fx:String>12pm</fx:String>
					<fx:String>1pm</fx:String>
					<fx:String>2pm</fx:String>
					<fx:String>3pm</fx:String>
					<fx:String>4pm</fx:String>
					<fx:String>5pm</fx:String>
					<fx:String>6pm</fx:String>
				</mx:ArrayList>
			</s:dataProvider>
		</s:DropDownList>
	</s:HGroup>
	<s:Line width="100%">
		<s:stroke><s:SolidColorStroke color="#aaaaaa" weight="1"/></s:stroke>
	</s:Line>
	<s:HGroup>
		<s:VGroup id="commandButtons" gap="1"/>
		<s:Line height="100%">
			<s:stroke><s:SolidColorStroke color="#aaaaaa" weight="1"/></s:stroke>
		</s:Line>
		<s:TileGroup id="itemButtons" horizontalGap="1" verticalGap="1"/>
		<s:Line height="100%">
			<s:stroke><s:SolidColorStroke color="#aaaaaa" weight="1"/></s:stroke>
		</s:Line>
		<s:TileGroup id="colorButtons" horizontalGap="1" verticalGap="1"/>
	</s:HGroup>
	<s:Line width="100%">
		<s:stroke><s:SolidColorStroke color="#aaaaaa" weight="1"/></s:stroke>
	</s:Line>
	<s:HGroup>
		<s:Label text="Ticket:"/>
		<s:Label id="orderID" styleName="numberLabel"/>
		<s:Line height="100%">
			<s:stroke><s:SolidColorStroke color="#aaaaaa" weight="1"/></s:stroke>
		</s:Line>
		<s:Label text="Items:"/>
		<s:Label id="items" text="0" styleName="numberLabel"/>
		<s:Line height="100%">
			<s:stroke><s:SolidColorStroke color="#aaaaaa" weight="1"/></s:stroke>
		</s:Line>
		<s:Label text="Total:"/>
		<s:Label id="total" text="0" styleName="numberLabel"/>
		<s:Line height="100%">
			<s:stroke><s:SolidColorStroke color="#aaaaaa" weight="1"/></s:stroke>
		</s:Line>
		<s:Label text="Paid:"/>
		<s:Label id="paid" text="0" styleName="numberLabel" color="#00ff00"/>
	</s:HGroup>
	<s:Line width="100%">
		<s:stroke><s:SolidColorStroke color="#aaaaaa" weight="1"/></s:stroke>
	</s:Line>
	<local:OrderItemList id="itemList" width="100%" height="100%"/>
	<fx:Script>
		<![CDATA[
			import data.Data;
			import data.Order;
			
			import flash.text.engine.FontWeight;
			
			import mx.binding.utils.BindingUtils;
			import mx.containers.TitleWindow;
			import mx.controls.Alert;
			import mx.core.IFlexDisplayObject;
			import mx.core.IUIComponent;
			import mx.managers.PopUpManager;
			
			import spark.components.Button;
			
			import ui.Dialog;
			import ui.MultilineButtonSkin;
			
			private var _quantityButtons:Vector.<IUIComponent> = new Vector.<IUIComponent>(2);
			private var _commandButtonInfo:Array =
			[
				{ name: "-PAY-", color: 0x0000cc, handler: onPay },
				{ name: "PRINT TAG", color: 0x0000ff, handler: onPrintTag },
				{ name: "PRINT RCPT", color: 0x0000ff, handler: onPrintReceipt },
				{ name: "ITEM + 1", color: 0x00aa00, handler: onIncItem },
				{ name: "ITEM - 1", color: 0xbb0000, handler: onDecItem },
				{ name: "SAVE", color: 0x00aa00, handler: onSave }
			];
			private function onInit():void
			{
				// Command buttons ///////////////////////////////////////////////
				// KAI: why not just do this as mxml?
				// KAI: doesn't this belong in createChildren?
				for each (var cmd:Object in _commandButtonInfo)
				{
					var cmdButton:Button = new Button;
					cmdButton.width = 125;
					cmdButton.height = 50;
					cmdButton.setStyle("color", cmd.color);
					cmdButton.setStyle("fontWeight", FontWeight.BOLD);
					cmdButton.label = cmd.name;

					if (cmd.handler)
					{
						cmdButton.addEventListener(MouseEvent.CLICK, cmd.handler, false, 0, true);
						if (cmd.handler == onDecItem)
						{
							_quantityButtons[0] = cmdButton;
						}
						else if (cmd.handler == onIncItem)
						{
							_quantityButtons[1] = cmdButton;
						}
					}
					commandButtons.addElement(cmdButton);
				}
				
				// Item buttons ////////////////////////////////////////////////
				for each (var item:Object in Data.instance.items)
				{
					var button:Button = new Button;
					button.setStyle("skinClass", MultilineButtonSkin);
					button.label = item.name + "\n" + Utils.currencyFormatter.format(item.price);
					button.height = 100;
					button.width = 100;
					button.name = item.id;
					button.addEventListener(MouseEvent.CLICK, onItem, false, 0, true);

					itemButtons.addElement(button);
				}
				
				BindingUtils.bindSetter(onSelectionUpdate, itemList.grid, "selectedIndex");
				
				// Colors and properties //////////////////////////////////
				for each (var color:Object in Data.instance.colors.source)
				{
					var colorButton:Button = new Button;
					colorButton.setStyle("color", color.color);
					colorButton.setStyle("fontWeight", FontWeight.BOLD);
					colorButton.label = color.name;
					colorButton.height = 50;
					colorButton.width = 100;
					colorButton.name = color.name;
					colorButton.addEventListener(MouseEvent.CLICK, onColorProperty, false, 0, true);
					
					colorButtons.addElement(colorButton);
				}
				for each (var pattern:String in Data.instance.patterns.source)
				{
					var patternButton:Button = new Button;
					patternButton.setStyle("fontSize", 12);
					patternButton.label = pattern;
					patternButton.height = 50;
					patternButton.width = 100;
					patternButton.name = pattern;
					patternButton.addEventListener(MouseEvent.CLICK, onColorProperty, false, 0, true);
					
					colorButtons.addElement(patternButton);
				}
				date.selectedDate = new Date;

				if (!_order)
				{
					newOrder();
				}
			} 
			private function newOrder():void
			{
				order = Data.instance.createOrder(0, new Date().date, "12pm", []);
			}
			private function onSave(e:Event):void
			{
				if (!_order.customerID)
				{
					Alert.show("Choose customer before saving", "Customer Missing");
				}
				else
				{
					Data.instance.orders.addItem(order);
					newOrder();					
				}
			}
			private function onPay(e:Event):void
			{
				var pay:PayUI = new PayUI;
				pay.total = _order.total;
				pay.owing = _order.total - _order.paid;
					
				var popup:Dialog = new Dialog;
				popup.bodyContent = pay;
				popup.autoClose = true;
				popup.title = "Pay";
				popup.completeLabel = "PAY";
				
				var dobj:DisplayObject = DisplayObject(e.currentTarget);
				var dest:Point = globalToLocal(dobj.localToGlobal(new Point(dobj.x + dobj.width, dobj.y + dobj.height)));
				
				popup.x = dest.x;
				popup.y = dest.y;
				
				PopUpManager.addPopUp(popup, this, true);
				popup.addEventListener(Event.COMPLETE, onPayComplete, false, 0, true);
			}
			private function onPayComplete(e:Event):void
			{
				const pay:PayUI = PayUI(Dialog(e.currentTarget).bodyContent);
				const change:Number = pay.paying - pay.owing; 

				_order.paid = Math.min(_order.total, _order.paid + pay.paying);
				
				updateTotal();

				if (change > 0)
				{
					Alert.show("Change of " + Utils.currencyFormatter.format(change) + " goes to customer.", "Change");
				}
			}
			private function onPrintTag(e:Event):void
			{
				var label:Label = new Label;
				label.height = 100;
				label.setStyle("verticalAlign", "middle");
				label.text = "---Printing tag " + _order.id + " for " + customer.text + "---";
				
				var popup:Dialog = new Dialog;
				popup.bodyContent = label;
				popup.autoClose = true;
				
				PopUpManager.addPopUp(popup, this);
				PopUpManager.centerPopUp(popup);
			}
			private function onPrintReceipt(e:Event):void
			{
				var label:Label = new Label;
				label.height = 100;
				label.setStyle("verticalAlign", "middle");
				label.text = "---Printing receipt " + _order.id + " for " + customer.text + " " + total.text + "---";
				
				var popup:Dialog = new Dialog;
				popup.bodyContent = label;
				popup.autoClose = true;
				
				PopUpManager.addPopUp(popup, this);
				PopUpManager.centerPopUp(popup);
			}
			private function onSelectionUpdate(foo:int):void
			{
				_quantityButtons[0].enabled = _quantityButtons[1].enabled = colorButtons.enabled = foo >= 0;
			}
			private function onItem(e:Event):void
			{
				var index:int = 0;
				for each (var item:Object in Data.instance.items.source)
				{
					if (item.id == parseInt(e.target.name))
					{
						_order.addItem(item);

						itemList.selectedIndex = _order.items.length - 1;
						updateTotal();
						break;
					}
					++index;
				}
			}
			private function onIncItem(e:Event):void
			{
				_order.incItem(itemList.selectedIndex);
				updateTotal();
			}
			private function onDecItem(e:Event):void
			{
				_order.decItem(itemList.selectedIndex);
				updateTotal();
				
				onSelectionUpdate(itemList.selectedIndex);
			}
			private function onColorProperty(e:Event):void
			{
				if (itemList.selectedIndex >= 0)
				{
					const color:String = IFlexDisplayObject(e.target).name;
					_order.addProperty(itemList.selectedIndex, color);
				}
			}
			private function updateTotal():void
			{
				items.text = String(_order.items.length);
				total.text = Utils.currencyFormatter.format(_order.total);
				paid.text = Utils.currencyFormatter.format(_order.paid);
				
				paid.setStyle("color", order.total == _order.paid ? 0x00ee00 : 0xff0000);
			}

			private var _customerList:CustomerList = new CustomerList;
			private var _currentPopup:IFlexDisplayObject;
			private function onSelectCustomer():void
			{
				var popup:Dialog = new Dialog;
				popup.title = "Choose Customer";
				popup.width = 700;
				popup.height = 500;
				_customerList.percentHeight = 100;
				_customerList.percentWidth = 100;
				_customerList.doubleClickEdits = false;
				popup.bodyContent = _customerList;
				

				PopUpManager.addPopUp(popup, this, true);
				PopUpManager.centerPopUp(popup);
				_currentPopup = popup;

				popup.addEventListener(Event.CLOSE, onCustomerChosen, false, 0, true);
				popup.addEventListener(Event.COMPLETE, onCustomerChosen, false, 0, true);
			}
			static private function formatCustomer(customerObj:Object):String
			{
				return customerObj.last + ", " + customerObj.first + " " + customerObj.phone;
			}
			private function onCustomerChosen(e:Event):void
			{
				if (e.type == Event.COMPLETE)
				{
					const customerObj:Object = _customerList.selectedCustomer;
					if (customerObj)
					{
						customer.text = formatCustomer(customerObj);
						_order.customerID = customerObj.id;
					}
				}
				PopUpManager.removePopUp(_currentPopup);
				_currentPopup = null;
			}
			private function dateFormatter(date:Date):String
			{
				return date ? date.toDateString() : "";
			}
			
			private var _order:Order;
			public function set order(o:Order):void
			{
				_order = o;

				orderID.text = String(o.id);
				customer.text = o.customerID ? formatCustomer(Data.instance.getCustomer(o.customerID)) : null;
				
				var d:Date = new Date();
				d.date = o.date;

				date.selectedDate = d;
				time.selectedItem = o.time;
				itemList.items = o.items;
				
				updateTotal();
			}
			public function get order():Order
			{
				//KAI: better binding could get rid of this
				_order.date = date.selectedDate.date;
				_order.time = time.selectedItem;
				return _order;
			}
		]]>
	</fx:Script>
</s:VGroup>
